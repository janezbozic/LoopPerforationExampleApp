plugins {
    id 'com.android.library'
}

android {
    compileSdkVersion 30
    buildToolsVersion '30.0.3'
    ndkPath "/mnt/DataLinux/ndk/out/linux/android-ndk-r23-canary" //Path to my local ndk build with perforation pass

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
}

task compileAidlNdk() {
    //Building AIDL specifications for import in our project
    doLast {
        def aidl = [android.sdkDirectory, 'build-tools', android.buildToolsVersion,
                    'aidl'].join(File.separator)

        def outDir = [projectDir.absolutePath, 'src', 'main', 'cpp', 'aidl'].join(File.separator)

        def headerOutDir = [projectDir.absolutePath, 'src', 'main', 'cpp', 'includes'].join(File.separator)

        def searchPathForImports = [projectDir.absolutePath, 'src', 'main', 'aidl'].join(File.separator)

        def aidlFile = [projectDir.absolutePath, 'src', 'main', 'aidl',
                         'com', 'example', "IMyService.aidl"].join(File.separator)

        exec {
            executable(aidl)
            args('--lang=ndk', '-o', outDir, '-h', headerOutDir, '-I', searchPathForImports, aidlFile)
        }
    }
}

afterEvaluate {
    preBuild.dependsOn(compileAidlNdk)
}
